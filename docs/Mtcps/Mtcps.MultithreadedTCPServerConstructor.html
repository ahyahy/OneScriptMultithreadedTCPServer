<!DOCTYPE html>
<html lang="ru">
<head>
    <title>MultithreadedTCPServer Constructor</title>
    <meta charset="UTF-8">
    <link rel="stylesheet" type="text/css" href="mainstyle.css">
    <script defer src="mobilstyle.js"></script>
</head>
<body id=bodyID class=dtBODY>
    <div id=nsbanner>
        <div id=bannerrow1>
            <table class=bannerparthead cellSpacing=0>
                <tbody>
                    <tr id=hdr>
                        <td class=runninghead></td>
                        <td class=product></td>
                    </tr>
                </tbody>
            </table>
        </div>
        <div id=TitleRow>
            <h1 class=dtH1>МногопоточныйTCPСервер (MultithreadedTCPServer)&nbsp;Конструктор</h1>
        </div>
    </div>
    <div id=nstext>
        <h4 class=dtH4>Синтаксис</h4>
        <p>МногопоточныйTCPСервер(&lt;НомерПорта&gt;)</p>
        <h4 class=dtH4>Параметры</h4>
        <dl>
            <dt><I>НомерПорта</I> (обязательный)</dt>
            <dd>Тип: Число.</dd>
            <dd>Прослушиваемый TCP-порт для входящих запросов на подключение на сервере.</dd>
		</dl>
        <h4 class=dtH4>Описание</h4>
        <p>Создаёт новый экземпляр класса <b>МногопоточныйTCPСервер&nbsp;(MultithreadedTCPServer)</b>.</p>
        <h4 class=dtH4>Примечание</h4>
        <p>
			Цикл ожидания событий сервера остановит выполнение скрипта.
			Если нужно продолжение выполнения скрипта после запуска сервера создавайте сервер 
			через метод <b>ФоновыйМногопоточныйСервер (BackgroundMultithreadedServer)</b>.
		</p>
        <h4 class=dtH4>Пример</h4>
        <pre class=code>
Сервер = МС.МногопоточныйСервер(8080);
</pre>
        <details>
            <summary>Полный пример кода</summary>
            <pre class=code>
<button id="copy1" type="button" style="font-size:100%;">Копировать</button>
<hr style="border-color: lightgray;"><div id="cont1">
// В качестве клиента удобно будет использовать TCPКлиент (TCPClient)
Перем МС, Сервер;

Процедура ПриПодключенииКлиента() Экспорт
	АргументыСобытия = МС.АргументыСобытия;
	Сообщить("Клиент подключился: " + АргументыСобытия.ИдентификаторКлиента);
	Сообщить("Активных клиентов: " + Сервер.КоличествоАктивныхКлиентов);
	Сообщить("========================");
КонецПроцедуры

Процедура ВывестиСообщение(Данные, ИдентификаторКлиента)
	Попытка
		РазмерДанных = Данные.ПрочитатьЦелое32(0);
		ДанныеСообщения = Данные.Прочитать(4, РазмерДанных);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Сообщение от " + ИдентификаторКлиента + " (" + РазмерДанных + " байт):" + Символы.ПС + ОтдельноеСообщение);
		Если (Данные.Размер - 4 - РазмерДанных) > 0 Тогда
			ОстатокДанных = Данные.Прочитать(4 + РазмерДанных, Данные.Размер - 4 - РазмерДанных);
			ВывестиСообщение(ОстатокДанных, ИдентификаторКлиента);
		КонецЕсли;
	Исключение
		ДанныеСообщения = Данные.Прочитать(0, Данные.Размер);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Сообщение от " + ИдентификаторКлиента + " (" + Данные.Размер + " байт):" + Символы.ПС + ОтдельноеСообщение);
	КонецПопытки;
КонецПроцедуры
   
Процедура СерверПолучилДанные() Экспорт
	АргументыСобытия = МС.АргументыСобытия;
	Данные = АргументыСобытия.Данные;
	// Данные, полученные сервером, доступны нам как тип БуферДвоичныхДанных (BinaryDataBuffer).
	// Если с сервером будет взаимодействовать клиент, который формирует сообщения по оговоренным заранее правилам, 
	// появляется возможность делить данные на отдельные сообщения и кроме того знать тип данных отдельного сообщения.
	// Как частный случай можно на стороне клиента отправлять в сетевой поток сначала размер сообщения (четыре байта) 
	// а затем само сообщение. В этом примере так и сделано.
	// Разобъем полученные сервером данные на отдельные сообщения.
	ВывестиСообщение(Данные, АргументыСобытия.ИдентификаторКлиента);
	
	// Отправим всем строку.
	Сервер.ОтправитьВсем(ПолучитьСтрокуИзБуфераДвоичныхДанных(Данные));
	Сообщить("========================");
КонецПроцедуры
   
Процедура ПриОтключенииКлиента() Экспорт
	АргументыСобытия = МС.АргументыСобытия;
	Сообщить("Клиент отключился: " + АргументыСобытия.ИдентификаторКлиента);
	Сообщить("Активных клиентов: " + Сервер.КоличествоАктивныхКлиентов);
	Сообщить("========================");
КонецПроцедуры
   
Процедура ПриОшибкеСервера() Экспорт
	АргументыСобытия = МС.АргументыСобытия;
	Сообщить("ОшибкаСервера: " + АргументыСобытия.ОшибкаСервера);
	Сообщить("========================");
КонецПроцедуры

ПодключитьВнешнююКомпоненту("C:\444\111\MultithreadedTCPServer\MultithreadedTCPServer\bin\Debug\MultithreadedTCPServer.dll");
МС = Новый МногопоточныйTCPСерверДляОдноСкрипта();

Сервер = МС.МногопоточныйСервер(8080);
Сервер.ПриОшибкеСервера = МС.Действие(ЭтотОбъект, "ПриОшибкеСервера");
Сервер.ПриОтключенииКлиента = МС.Действие(ЭтотОбъект, "ПриОтключенииКлиента");
Сервер.СерверПолучилДанные = МС.Действие(ЭтотОбъект, "СерверПолучилДанные");
Сервер.ПриПодключенииКлиента = МС.Действие(ЭтотОбъект, "ПриПодключенииКлиента");
Сервер.Начать();

Пока МС.Продолжать Цикл
	МС.ПолучитьСобытие().Выполнить();
КонецЦикла;
</div>
</pre>
        </details>
        <p></p>
        <h4 class=dtH4>Смотрите также</h4>
        <p><a href="Mtcps.html">Библиотека&nbsp;MultithreadedTCPServer</a></p>
    </div>
</body>
</html>
