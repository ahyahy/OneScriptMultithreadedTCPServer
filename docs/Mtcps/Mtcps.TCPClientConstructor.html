<!DOCTYPE html>
<html lang="ru">
<head>
    <title>TCPClient Constructor</title>
    <meta charset="UTF-8">
    <link rel="stylesheet" type="text/css" href="mainstyle.css">
    <script defer src="mobilstyle.js"></script>
</head>
<body id=bodyID class=dtBODY>
    <div id=nsbanner>
        <div id=bannerrow1>
            <table class=bannerparthead cellSpacing=0>
                <tbody>
                    <tr id=hdr>
                        <td class=runninghead></td>
                        <td class=product></td>
                    </tr>
                </tbody>
            </table>
        </div>
        <div id=TitleRow>
            <h1 class=dtH1>TCPКлиент (TCPClient)&nbsp;Конструктор</h1>
        </div>
    </div>
    <div id=nstext>
        <h4 class=dtH4>Синтаксис</h4>
        <p>TCPКлиент()</p>
        <p>TCPКлиент(&lt;ИмяУзла&gt;, &lt;НомерПорта&gt;)</p>
        <h4 class=dtH4>Параметры</h4>
        <dl>
            <dt><i>ИмяУзла</i> (обязательный)</dt>
            <dd>Тип: Строка.</dd>
            <dd>
                
            </dd>
            <dt><i>НомерПорта</i> (обязательный)</dt>
            <dd>Тип: Число.</dd>
            <dd>
                Порт, на котором производится ожидание входящих попыток подключений.
            </dd>
        </dl>
        <h4 class=dtH4>Описание</h4>
        <p>Создаёт новый экземпляр класса <b>TCPКлиент&nbsp;(TCPClient)</b> и если переданы параметры, подключает его к указанному порту заданного узла.</p>
        <h4 class=dtH4>Примечание</h4>
        <p>
			Для тестировании TCP клиента удобно будет использовать <a href="MtcpsEchoServer.html">Сервер в "Эхо" режиме.</a>
		</p>
        <h4 class=dtH4>Пример</h4>
        <pre class=code>
Клиент = МС.TCPКлиент();
</pre>
        <details>
            <summary>Полный пример кода</summary>
            <pre class=code>
<button id="copy1" type="button" style="font-size:100%;">Копировать</button>
<hr style="border-color: lightgray;"><div id="cont1">
Перем МС;

Процедура ВывестиСообщение(Данные)
	Попытка	
		// В случае передачи в потоке перед самим сообщением размера сообщения используем этот вариант.
		РазмерДанных = Данные.ПрочитатьЦелое32(0);
		ДанныеСообщения = Данные.Прочитать(4, РазмерДанных);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Получено сообщение: " + " (" + РазмерДанных + " байт):" + Символы.ПС + ОтдельноеСообщение);
		Если (Данные.Размер - 4 - РазмерДанных) > 0 Тогда
			ОстатокДанных = Данные.Прочитать(4 + РазмерДанных, Данные.Размер - 4 - РазмерДанных);
			// Продолжаем делить данные на отдельные сообщения.
			ВывестиСообщение(ОстатокДанных);
		КонецЕсли;
	Исключение
		// В случае передачи в потоке никак не разделенных байтов используем этот вариант.
		ДанныеСообщения = Данные.Прочитать(0, Данные.Размер);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Получено сообщение: " + " (" + Данные.Размер + " байт):" + Символы.ПС + ОтдельноеСообщение);
	КонецПопытки;
	// Можно предусмотреть другие способы разделения потока байтов на отдельные сообщения, например посылка символа перевода строки.
КонецПроцедуры

Процедура КлиентПолучилДанные() Экспорт
	АргументыСобытия = МС.АргументыСобытия;
	Данные = АргументыСобытия.Данные;
	// Данные, полученные клиентом, доступны нам как тип БуферДвоичныхДанных (BinaryDataBuffer).
	// Если с клиентом будет взаимодействовать сервер, который формирует сообщения по оговоренным заранее правилам, 
	// появляется возможность делить данные на отдельные сообщения и кроме того знать тип данных отдельного сообщения.
	// Как частный случай можно на стороне сервера отправлять в сетевой поток сначала размер сообщения (четыре байта) 
	// а затем само сообщение.
	ВывестиСообщение(Данные);
	Сообщить("========================");
КонецПроцедуры

Процедура ЗапускКлиента(параметр1) Экспорт
    Контекст = Новый Структура();
    Контекст.Вставить("Клиент", параметр1);
	Стр = "
	|Перем ПотокСети1;
	|
	|Процедура ПроверитьСообщение()
	|    Клиент.ОбработатьКлиентПолучилДанные(ПотокСети1.ПрочитатьВБуферДвоичныхДанных());
	|КонецПроцедуры
	|
	|ПотокСети1 = Клиент.ПолучитьПоток();
	|
	|Пока Клиент.Подключен Цикл
	|    Если Не ПотокСети1.ДанныеДоступны Тогда
	|        Приостановить(100);
	|    Иначе
	|        ПроверитьСообщение();
	|    КонецЕсли;
	|КонецЦикла;
	|";
	ЗагрузитьСценарийИзСтроки(Стр, Контекст);
КонецПроцедуры


ПодключитьВнешнююКомпоненту("C:\444\111\MultithreadedTCPServer\MultithreadedTCPServer\bin\Debug\MultithreadedTCPServer.dll");
МС = Новый МногопоточныйTCPСерверДляОдноСкрипта();

Клиент = МС.TCPКлиент();
// Назначим обработчик события.
Клиент.КлиентПолучилДанные = МС.Действие(ЭтотОбъект, "КлиентПолучилДанные");

Клиент.Подключить("127.0.0.1", 8080);
// Подождем пока клиент подключится.
Пока Не Клиент.Подключен Цикл
	Приостановить(100);
КонецЦикла;

// Запустим клиента фоновым заданием.
МассивПараметров = Новый Массив(1);
МассивПараметров[0] = Клиент;
Задание = ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапускКлиента", МассивПараметров);
Приостановить(1000);

Сообщить("Подключен");
Сообщить("Печатайте и нажмите ENTER");

// // Можно отправлять данные.
// БуферОтправки = ПолучитьБуферДвоичныхДанныхИзСтроки("Серверу от клиента ПРИВЕТ!");
// Клиент.Отправить(БуферОтправки);

// Можно далее дополнять алгоритм сценария, фоновый TCP клиент не блокирует выполнение сценария.
// Не будем закрывать подключение и остановимся в цикле ввода сообщений.
Результат = Неопределено;
Пока ВвестиСтроку(Результат, , , ) Цикл
	Клиент.Отправить(ПолучитьБуферДвоичныхДанныхИзСтроки(Результат));
КонецЦикла;
</div>
</pre>
        </details>
        <p></p>
        <h4 class=dtH4>Смотрите также</h4>
        <p><a href="Mtcps.html">Библиотека&nbsp;MultithreadedTCPServer</a></p>
    </div>
</body>
</html>
